# coding: utf-8
# MaratisPlayer framework build Sconscript
# $LastChangedDate: 2012-09-05 21:17:54 +0100 (Wed, 05 Sep 2012) $
# $LastChangedBy: skaiware@gmail.com $


import os, sys, fnmatch
import pyutils
import thirdparty

Import('env')
env_local = env.Clone()

# include dirs
env_local.AppendUnique(CPPPATH = ['#MSDK/MCore/Includes',
                                  '#MSDK/MEngine/Includes',
                                  '#MSDK/MGui/Includes',
                                  '#Maratis/Common',
                                  '#Maratis/Common/_Implements'] )

if sys.platform=='linux2' or sys.platform=='linux3':
    env_local.AppendUnique(CPPPATH = ['/usr/include'] )

# lib dirs
env_local.AppendUnique(LIBPATH = ['../../MSDK/MaratisCore',
                                  '../../MSDK/MEngine',
                                  '../../MSDK/MCore',
                                  '../../MSDK/MGui',
                                  '../Common'] )
# needed libs
env_local.AppendUnique(LIBS = ['MaratisCommon',
                               'MEngine',
                               'MCore',
                               'MGui'] )

if sys.platform=='linux2' or sys.platform=='linux3':
    env_local.AppendUnique(LIBS = ['GL',
                                   'Xxf86vm','X11',
                                   'dl'] )

# add 3rdparty libraries parameters
thirdparty.npk_dev.addNPKDevToEnv(env_local)
thirdparty.lua.addLuaToEnv(env_local)
thirdparty.bullet.addBulletToEnv(env_local)
thirdparty.openal.addOpenALToEnv(env_local)
thirdparty.glee.addGleeToEnv(env_local)
thirdparty.devil.addDevilToEnv(env_local)
thirdparty.tinyxml.addTinyXmlToEnv(env_local)
thirdparty.libpng.addLibPngToEnv(env_local)
thirdparty.libjpeg.addLibJpegToEnv(env_local)
thirdparty.zlib.addZlibToEnv(env_local)
thirdparty.freetype.addFreetypeToEnv(env_local)
thirdparty.libsndfile.addLibSndFileToEnv(env_local)
thirdparty.opengl.addOpenGLToEnv(env_local) # must be after glee

if sys.platform=='darwin':
    thirdparty.cocoa.addCocoaToEnv(env_local)
if sys.platform=='win32' or sys.platform == 'cygwin':
    thirdparty.windows.addWindowsToEnv(env_local)

#    env_local.AppendUnique(LIBS = ['winmm', 'wininet', 'User32', 'Shell32', 'Gdi32', 'Comdlg32', 'glaux']) # WIN32 (libopengl32.a, libwinmm.a...)

# get list of files (recursive)
files = pyutils.getFilesRecursive(Dir('.').srcnode().abspath,'*.c*')

# build application
env_local.AppendUnique(RPATH = ['@loader_path/.'])
maratisplayer_exe  = env_local.Program('MaratisPlayer',files,PDB='MaratisPlayer.pdb')
Return('maratisplayer_exe')

